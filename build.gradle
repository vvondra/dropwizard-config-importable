apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

ext {
    dropwizardVersion = "1.0.5"
}

def mavenUser = hasProperty('sonatypeUsername') ? sonatypeUsername : System.getenv('SONATYPE_USER')
def mavenPass = hasProperty('sonatypePassword') ? sonatypePassword : System.getenv('SONATYPE_PASS')

group 'com.foodpanda'
version '0.1.0'

dependencies {
    compile 'io.dropwizard:dropwizard-configuration:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-jackson:' + dropwizardVersion

    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: mavenUser, password: mavenPass)
            }

            pom.project {
                name 'dropwizard-config-importable'
                packaging 'jar'
                description 'Use configuration imports in Dropwizard instead of copy-pasting files for each environment'
                url 'https://github.com/foodpanda/dropwizard-config-importable'

                scm {
                    url 'scm:git@github.com:foodpanda/dropwizard-config-importable.git'
                    connection 'scm:git@github.com:foodpanda/dropwizard-config-importable.git'
                    developerConnection 'scm:git@github.com:foodpanda/dropwizard-config-importable.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/foodpanda/dropwizard-config-importable/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'vvondra'
                        name 'Vojtěch Vondra'
                    }
                    developer {
                        id 'aelesbao'
                        name 'Augusto Elesbão'
                    }
                }
            }
        }
    }
}
